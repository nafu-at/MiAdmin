/*
 * Copyright 2020 PandaSoft.Dev Social Networks.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
    id 'java'
    id 'maven'
    id 'com.github.johnrengelman.shadow' version '5.2.0'
}

group 'online.pandasoft'
version '0.0.4'

sourceCompatibility = 11
targetCompatibility = 11
compileJava.options.encoding = 'UTF-8'

jar {
    manifest {
        attributes("Main-Class": "online.pandasoft.miadmin.core.Main",
                "Implementation-Title": "MiAdmine",
                "Implementation-Version": getArchiveVersion().get()
        )
    }
}

configurations {
    deployerJars
}

task sourcesJar(type: Jar, dependsOn: 'classes') {
    from sourceSets.main.allSource
    classifier = 'sources'
}
task javadocJar(type: Jar, dependsOn: 'javadoc') {
    from javadoc.destinationDir
    classifier = 'javadoc'
}
javadoc {
    options.charSet = 'UTF-8'
    options.encoding = 'UTF-8'
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

uploadArchives {
    repositories.mavenDeployer {
        configuration = configurations.deployerJars
        repository(id: 'pandasoft', url: 'ftp://sv8276.xserver.jp/') {
            authentication(userName: "${project.mavenUser}", password: "${project.mavenPassword}")
        }
        pom.project {
            inceptionYear '2020'
            licenses {
                license {
                    name 'The Apache Software License, Version 2.0'
                    url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    distribution 'repo'
                }
            }
        }
    }
}


repositories {
    mavenCentral()
}

dependencies {
    deployerJars 'org.apache.maven.wagon:wagon-ftp:3.3.4'
    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompileOnly 'org.projectlombok:lombok:1.18.10'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.10'

    // Logger
    implementation 'org.slf4j:slf4j-api:1.7.30'
    implementation 'ch.qos.logback:logback-core:1.2.3'
    implementation 'ch.qos.logback:logback-classic:1.2.3'
    compileOnly 'org.projectlombok:lombok:1.18.10'
    annotationProcessor 'org.projectlombok:lombok:1.18.10'
    implementation 'io.sentry:sentry-logback:1.7.30'

    // WebClient
    implementation 'com.squareup.okhttp3:okhttp:4.3.1'

    // Parser
    implementation 'com.google.code.gson:gson:2.8.6'
    implementation 'com.fasterxml.jackson.core:jackson-core:2.10.2'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.10.2'
    implementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:2.10.2'

    // Commons Library
    implementation 'commons-io:commons-io:2.6'
    implementation 'commons-codec:commons-codec:1.14'
    implementation 'org.apache.commons:commons-lang3:3.9'
    implementation 'org.apache.commons:commons-collections4:4.4'

    // SQL
    implementation 'com.zaxxer:HikariCP:3.4.2'
    implementation 'mysql:mysql-connector-java:8.0.18'
    implementation 'org.mariadb.jdbc:mariadb-java-client:2.5.3'

    // Other
    implementation 'de.vandermeer:asciitable:0.3.2'
}